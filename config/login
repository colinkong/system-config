#
# $HOME/.login for csh/tcsh shells
#
# 1) Create "$HOME/.login-2bash" to auto switch to bash shell
# 2) Create "$HOME/.login-local" for local settings
#

##set verbose

if ( $?TERM && "`\tty | \grep -v ' '`" != "" ) then
    if ( -f $HOME/.login-2bash && -f /bin/bash ) then
        setenv ENV $HOME/.profile; exec /bin/bash -l
    endif

    # Terminal Multiplexer
    if ( -x "/usr/bin/tmux" ) then
        if ( $?TMUX ) then
            if ( ! $?MYSCREEN ) then
                setenv MYSCREEN "tmux:`tmux display-message -p '#S'`"
                unsetenv MYTTY
            endif
            alias logout 'tmux detach'
        else if ( "$TERM" == "screen" ) then
            setenv MYSCREEN "???"
            alias tmux='echo "sessions should be nested with care, already running multiplexer" #'
        endif
    endif

    # Removing soft shell limit
    foreach _limit ( `limit | \egrep -v "unlimited|-" | \awk '{print $1}'` )
        if ( "`limit $_limit unlimited`" == "" ) then
            limit $_limit unlimited
        endif
    end

    # Session control
    setenv MYHNAME `uname -n | tr '[A-Z]' '[a-z]' | cut -f1 -d"."`
    setenv MYUNAME `id | sed -e 's/^[^(]*(\([^)]*\)).*$/\1/'`
    if ( $?tcsh ) then
        setenv MYSHELL "TCSH"
        set prompt="%{\033[01;32m%}%n@%m:%{\033[01;34m%}%~%{\033[00m%} % "
    else
        setenv MYSHELL "CSH"
        set prompt="$MYUNAME@${MYHNAME} % "
    endif
    if ( $?SSH_CONNECTION ) then
        setenv MYSHELL "$MYSHELL remote"
    endif
    if ( $?MYTTY ) then
        alias logout 'echo Use \"exit\" to exit the shell.'
    else
        setenv MYSHELL "$MYSHELL login"
        if ( "$TERM" == "dumb" ) then
            setenv MYTTY "/dev/???"
        else
            setenv MYTTY `\tty`
            (mesg y) >& /dev/null
            set autologout=0
            stty erase "^?"  #  alternative
            stty intr 
            stty susp 
        endif
        if ( $?SSH_CONNECTION ) then
            alias exit 'echo Use \"logout\" to leave the remote login shell.'
        else
            alias exit 'echo Use \"logout\" to leave the login shell.'
        endif
        alias exec ''
    endif

    # General stuff
    umask 027
    setenv LC_COLLATE C
    setenv _POSIX2_VERSION 199209
    setenv PYTHONDONTWRITEBYTECODE 1
    setenv TMP /tmp; setenv TMPDIR $TMP; rm -rf $HOME/tmp >& /dev/null
    setenv WINEDITOR vi

    set history=256
    alias df "df -k"
    alias du "du -k"
    alias ftp "ftp -i -v"
    alias h history 256
    alias ls "ls -C"
    alias quota "quota -v"
    if ( -x /usr/bin/vim ) then
        alias vi "vim -N -n -i NONE -T xterm"
    endif
    if ( -x /usr/sbin/ping ) then
        alias ping "/usr/sbin/ping"
    else if ( -x /usr/etc/ping ) then
        alias ping "/usr/etc/ping"
    endif
    if ( -f "$HOME/.ssh/config" ) then
        if ( "`\ssh -version |& \grep '^OpenSSH_'`" != "" ) then
            alias scp "scp -F $HOME/.ssh/config -o ServerAliveInterval=1799"
        else
            alias scp "scp -F $HOME/.ssh/config"
        endif
    endif
    unalias cp cd.. d l ll la lsd mv p rm s which >& /dev/null

    # Save/load default settings (PATH, MANPATH, LM_LICENSE_FILE, DSOPATH)
    if ( ! $?BASE_MANPATH ) then
        if ( ! $?MANPATH ) then
            setenv MANPATH :
        endif
        if ( ! $?LM_LICENSE_FILE ) then
            setenv LM_LICENSE_FILE :
        endif
        if ( ! $?LD_LIBRARY_PATH ) then
            setenv LD_LIBRARY_PATH :
        endif
        setenv BASE_PATH $PATH
        setenv BASE_MANPATH $MANPATH
        setenv BASE_LM_LICENSE_FILE $LM_LICENSE_FILE
        setenv BASE_LD_LIBRARY_PATH $LD_LIBRARY_PATH
    else
        setenv PATH $BASE_PATH
        setenv MANPATH $BASE_MANPATH
        setenv LM_LICENSE_FILE $BASE_LM_LICENSE_FILE
        setenv LD_LIBRARY_PATH $BASE_LD_LIBRARY_PATH
    endif

    # Change to $HOME/Desktop
    mkdir $HOME/Desktop >& /dev/null
    chmod go= $HOME/Desktop .??*/* >& /dev/null
    if ( "$PWD" == "$HOME" ) then
        cd "$HOME/Desktop"
    endif

    # mkinst, cdinst, mksrc, cdsrc, mktest, cdtest, scd
    foreach _dir (inst src test)
        alias mk$_dir 'setenv cd'$_dir' "$PWD"; echo "cd'$_dir'=$cd'$_dir'"'
        alias cd$_dir 'echo "cd \"$cd'$_dir'\""; cd "$cd'$_dir'"'
        if ( ! $?cdtest ) then
            setenv cd$_dir "$PWD"
        endif
    end
    alias scd 'env | egrep "^cd(inst|src|test)=" | sort'

    # Platform specific
    switch (`uname`)
    case Darwin:
        if ( -d /usr/local/opt/coreutils/libexec/gnubin ) then
            setenv PATH "/usr/local/opt/coreutils/libexec/gnubin:${PATH}"
            alias ls "ls -C --time-style=long-iso --color=always"
        else
            alias ls "ls -C -G"
        endif
        breaksw

    case HP-UX:
        alias df "bdf"
        breaksw

    case Linux:
    case *NT*:
        if ( "`\ls --help |& \grep '^ *--time-style'`" != "" ) then
            alias ls "ls -C --time-style=long-iso --color=always"
        else
            alias ls "ls -C --color=always"
        endif
        setenv LS_COLORS ""
        breaksw
    endsw
    setenv PATH "$HOME/software/bin:/usr/local/bin:$PATH"

    # Applying local settings
    if ( -f $HOME/.login-local ) then
        source $HOME/.login-local
    endif

    echo
    if ( $?MYSCREEN ) then
        echo "$MYSHELL shell ($MYTTY, screen=$MYSCREEN)"
    else
        echo "$MYSHELL shell ($MYTTY)"
    endif
    echo

endif
