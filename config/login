#
# $HOME/.login for csh/tcsh shells
#
# 1) Create "$HOME/.login-2bash" to auto switch to bash shell
# 2) Create "$HOME/.login-local" for local settings
#

##set verbose

if ( $?TERM && "`\tty | \grep -v ' '`" != "" ) then
    if ( -f $HOME/.login-2bash && -f /bin/bash ) then
        setenv ENV $HOME/.profile; exec /bin/bash -l
    endif
    echo ""
    echo "Processing Startup script ($HOME/.login)"

    # Session control
    setenv MYHNAME `uname -n | tr '[A-Z]' '[a-z]' | cut -f1 -d"."`
    setenv MYUNAME `id | sed -e 's/^[^(]*(\([^)]*\)).*$/\1/'`
    if ( $?MYTTY ) then
        alias logout 'echo Use \"exit\" to exit the shell.'
    else
        if ( "$TERM" == "dumb" ) then
            setenv MYTTY "/dev/???"
        else
            setenv MYTTY `\tty`
            (mesg y) >& /dev/null
            set autologout=0
            stty erase "^?"  #  alternative
            stty intr 
            stty susp 
        endif
        alias exec ''
        alias exit 'echo Use \"logout\" to leave the login shell.'
        echo "Running    Login shell /bin/tcsh (TC Shell) on $MYTTY ($TERM)"
    endif
    if ( $?tcsh ) then
        set prompt="%{\033[01;32m%}%n@%m:%{\033[01;34m%}%~%{\033[00m%} % "
    else
        set prompt="$MYUNAME@${MYHNAME} % "
    endif

    # General stuff
    umask 027
    setenv LC_COLLATE C
    setenv _POSIX2_VERSION 199209
    setenv PYTHONDONTWRITEBYTECODE 1
    setenv TMP /tmp; setenv TMPDIR $TMP; rm -rf $HOME/tmp >& /dev/null
    setenv WINEDITOR vi

    set history=256
    alias df "df -k"
    alias du "du -k"
    alias ftp "ftp -i -v"
    alias h history 256
    alias ls "ls -C"
    alias quota "quota -v"
    if ( -x /usr/bin/vim ) then
        alias vi "vim -N -n -i NONE -T xterm"
    endif
    if ( -x /usr/sbin/ping ) then
        alias ping "/usr/sbin/ping"
    else if ( -x /usr/etc/ping ) then
        alias ping "/usr/etc/ping"
    endif
    if ( -f "$HOME/.ssh/config" ) then
        if ( "`\ssh -version |& \grep '^OpenSSH_'`" != "" ) then
            alias scp "scp -F $HOME/.ssh/config -o ServerAliveInterval=1799"
        else
            alias scp "scp -F $HOME/.ssh/config"
        endif
    endif
    unalias cp cd.. d l ll la lsd mv p rm s which >& /dev/null

    # Save/Load default settings (PATH, MANPATH, LM_LICENSE_FILE, DSOPATH)
    if ( ! $?BASE_MANPATH ) then
        if ( ! $?MANPATH ) then
            setenv MANPATH :
        endif
        if ( ! $?LM_LICENSE_FILE ) then
            setenv LM_LICENSE_FILE :
        endif
        if ( ! $?LD_LIBRARY_PATH ) then
            setenv LD_LIBRARY_PATH :
        endif
        setenv BASE_PATH $PATH
        setenv BASE_MANPATH $MANPATH
        setenv BASE_LM_LICENSE_FILE $LM_LICENSE_FILE
        setenv BASE_LD_LIBRARY_PATH $LD_LIBRARY_PATH
    else
        setenv PATH $BASE_PATH
        setenv MANPATH $BASE_MANPATH
        setenv LM_LICENSE_FILE $BASE_LM_LICENSE_FILE
        setenv LD_LIBRARY_PATH $BASE_LD_LIBRARY_PATH
    endif

    # Removing soft shell limit
    foreach _limit ( `limit | \egrep -v "unlimited|-" | \awk '{print $1}'` )
        if ( "`limit $_limit unlimited`" == "" ) then
            limit $_limit unlimited
        endif
    end

    # Change to $HOME/Desktop
    mkdir $HOME/Desktop >& /dev/null
    chmod go= $HOME/Desktop .??*/* >& /dev/null
    if ( "$PWD" == "$HOME" ) then
        cd "$HOME/Desktop"
    endif

    # cd[a-z], mk[a-z], scd
    if ( ! $?cdz ) then
        foreach DIR (a b c d e f g h i j k l m n o p q r s t u v w x y z)
            setenv cd$DIR "$PWD"
        end
    endif
    foreach _dir (a b c d e f g h i j k l m n o p q r s t u v w x y z)
        alias mk$_dir 'setenv cd'$_dir' "$PWD"; echo "cd'$_dir'=$cd'$_dir'"'
        alias cd$_dir 'echo "cd \"$cd'$_dir'\""; cd "$cd'$_dir'"'
    end
    alias scd 'env | grep "^cd[a-z]=" | grep -v "=$HOME/Desktop$" | sort'

    # Platform specific
    switch (`uname`)
    case Darwin:
        if ( -d /usr/local/opt/coreutils/libexec/gnubin ) then
            setenv PATH "/usr/local/opt/coreutils/libexec/gnubin:${PATH}"
            alias ls "ls -C --time-style=long-iso --color=always"
        else
            alias ls "ls -C -G"
        endif
        breaksw

    case HP-UX:
        alias df "bdf"
        breaksw

    case Linux:
    case *NT*:
        if ( "`\ls --help |& \grep '^ *--time-style'`" != "" ) then
            alias ls "ls -C --time-style=long-iso --color=always"
        else
            alias ls "ls -C --color=always"
        endif
        setenv LS_COLORS ""
        breaksw
    endsw
    setenv PATH "$HOME/software/bin:/usr/local/bin:$PATH"

    # Applying local settings
    if ( -f $HOME/.login-local ) then
        echo "Applying   Local settings ($HOME/.login-local)"
        source $HOME/.login-local
    endif
    echo ""

endif
